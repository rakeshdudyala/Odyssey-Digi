#@TYPE: Machine
#@NAME: ConnectCore 91 Development Kit
#@DESCRIPTION: Machine configuration for Digi's ConnectCore 91 DVK

DIGI_SOM = "ccimx91"
MACHINEOVERRIDES =. "mx91:${DIGI_FAMILY}:${DIGI_SOM}:"

# Include the machine configuration for Digi's ConnectCore 9 module
include conf/machine/include/ccimx9.inc

IMXBOOT_TARGETS = "flash_singleboot"

# U-Boot configurations
UBOOT_CONFIG ??= "ccimx91-dvk"
UBOOT_CONFIG[ccimx91-dvk] = "ccimx91-dvk_defconfig"

# Index of USB device for installer
UBOOT_INSTALL_USB_INDEX = "0"
# Index of microSD device for installer
UBOOT_INSTALL_SD_INDEX = "1"

# imx-boot configurations
ATF_PLATFORM = "imx91"
IMX_BOOT_SOC_TARGET = "iMX91"
IMXBOOT_TARGETS = "flash_singleboot"
SECO_FIRMWARE_NAME:ccimx91 = "mx91a0-ahab-container.img"

BOOTLOADER_SEEK_USERDATA = "32"

KERNEL_DEVICETREE ?= " \
    digi/ccimx91-dvk.dtb \
    digi/ccimx9-dvk_enet2.dtbo \
    digi/ccimx9-dvk_mikroe-accel2-click.dtbo \
    digi/ccimx9-dvk_mikroe-gyro-click.dtbo \
    digi/ccimx9_bt.dtbo \
    digi/ccimx9_bt-dtm.dtbo \
    digi/ccimx9_wifi.dtbo \
    ${@bb.utils.contains('DISTRO_FEATURES', 'tsn', 'digi/ccimx9-dvk_eqos-tsn.dtbo', '', d)} \
"
FIT_CONF_DEFAULT_DTB = "ccimx91-dvk.dtb"

# Default image for install scripts
DEFAULT_IMAGE_NAME ?= "core-image-base"

# Set this platform without graphical support
IS_HEADLESS = "true"

# Boot artifacts to be copied from the deploy dir to the installer ZIP
BOOTABLE_ARTIFACTS = " \
    imx-boot##SIGNED##-ccimx91-dvk.bin \
"

# Add secure enclave
SOC_TOOLS_TEST:append:mx91-nxp-bsp = " imx-secure-enclave"
# Add AFE support
NXPAFE_INSTALL:mx91-nxp-bsp = "nxp-afe nxp-afe-voiceseeker"
