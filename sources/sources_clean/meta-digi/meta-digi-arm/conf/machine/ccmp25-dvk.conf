#@TYPE: Machine
#@NAME: ccmp25-dvk
#@DESCRIPTION: Configuration for Digi ConnectCore MP25 DVK
#@NEEDED_BSPLAYERS: layers/meta-digi

# Include the machine configuration for Digi's ConnectCore MP2.
include conf/machine/include/ccmp2.inc

# Define specific familly common machine name
DIGI_SOM = "ccmp25"
MACHINEOVERRIDES =. "stm32mp2common:stm32mp25common:${DIGI_FAMILY}:${DIGI_SOM}:"

# =========================================================================
# Chip architecture
# =========================================================================
DEFAULTTUNE = "cortexa35"
include conf/machine/include/arm/armv8a/tune-cortexa35.inc

# =========================================================================
# boot device
# =========================================================================
# Configure the list of boards that enable EMMC/SDCARD
DEVICE_BOARD_ENABLE:EMMC   += "${STM32MP_DEVICETREE}"
DEVICE_BOARD_ENABLE:SDCARD += "${@bb.utils.contains('BOOTDEVICE_LABELS', 'sdcard', '${STM32MP_DEVICETREE}', '', d)}"

# =========================================================================
# U-Boot configs
# =========================================================================
# Remove STM32mp25 configs from ST layer
UBOOT_CONFIG:remove:stm32mp25common = " default_stm32mp25 "
# Last one is the default (the one the symlinks point at)
UBOOT_CONFIG = "ccmp25-dvk"
UBOOT_CONFIG[ccmp25-dvk] = "ccmp25-dvk_defconfig,,u-boot.dtb"
# List of U-Boot device tree to use
UBOOT_DEVICETREE = "${STM32MP_DEVICETREE}"
# Index of USB device for installer
UBOOT_INSTALL_USB_INDEX = "0"
# Index of microSD device for installer
UBOOT_INSTALL_SD_INDEX = "2"

# =========================================================================
# Machine settings
# =========================================================================
# Define list of devicetree per supported storage
STM32MP_DT_FILES_EMMC = "ccmp25-dvk"
STM32MP_DT_FILES_SDCARD = "${STM32MP_DT_FILES_EMMC}"
STM32MP_DT_FILES_USB = "${STM32MP_DT_FILES_EMMC}"

# Extra files for the board must be specified while ensuring the main DTB
# file for FIT image generation remains the first item.
STM32MP_KERNEL_DEVICETREE:ccmp25-dvk += " \
    ccmp25-dvk.dtb \
    ccmp25_bt.dtbo \
    ccmp25_wifi.dtbo \
    ccmp25-dvk_e55rb-i-mw346-c-mipi-dsi.dtbo \
    ccmp25-dvk_g101evn010-lvds.dtbo \
    ccmp25-dvk_ov5640-mipi-csi.dtbo \
    ccmp25-dvk_mikroe-accel2-click.dtbo \
    ccmp25-dvk_mikroe-gyro-click.dtbo \
    ccmp25-dvk_mikroe-i2c-to-spi-click.dtbo \
    ccmp25-dvk_mikroe-mcp2518fd-click.dtbo \
    ccmp25-dvk_n25q256a-spi-nor-flash.dtbo \
    ccmp25-dvk_nhd-3-5-640480ef-msxp-mipi-dsi.dtbo \
    ccmp25-dvk_usb-3-0-typec.dtbo \
"
FIT_CONF_DEFAULT_DTB = "ccmp25-dvk.dtb"

# =========================================================================
# optee
# =========================================================================
# Map OPTEE configuration to device tree list
OPTEE_DEVICETREE_optee = "${STM32MP_DEVICETREE}"

# =========================================================================
# Machine features
# =========================================================================
MACHINE_FEATURES += "splashscreen"
MACHINE_FEATURES += "watchdog"
MACHINE_FEATURES += "bluetooth"
MACHINE_FEATURES += "wifi"
MACHINE_FEATURES += "usbg0"

# GPU
MACHINE_FEATURES += "${@'gpu' if d.getVar('ACCEPT_EULA_'+d.getVar('MACHINE')) == '1' else ''}"
MACHINE_FEATURES += "openvx"
MACHINE_FEATURES += "opencl"
MACHINE_FEATURES += "vulkan"

# M33
MACHINE_FEATURES += "m33copro"

# Disable external dt
EXTERNAL_DT_ENABLED = "0"

# Flashlayout
# No metadata generated and supported for the moment
STM32MP_METADATA_DATA ?= ""

# GPU have vulkan
DISTRO_FEATURES:append = " ${@bb.utils.contains('DISTRO', 'openstlinux-eglfs', '', 'vulkan', d)} "
DISTRO_FEATURES:append = " opencl "

# =========================================================================
# Firmware
# =========================================================================
MACHINE_FIRMWARE:append = " firmware-murata-infineon firmware-murata-infineon-mfgtest"

MACHINE_EXTRA_RRECOMMENDS += " \
    ${MACHINE_FIRMWARE} \
"

# Default image for install scripts
DEFAULT_IMAGE_NAME ?= "dey-image-webkit"

# Wic files
WKS_FILES += " \
    ccmp2-1GB.wks.in \
"

# Wireless external module
HAS_WIFI_VIRTWLANS = "true"

# For populate_sdk, gcc-arm-none-eabi_11 has a libpython3 dependency, so we remove it.
ST_TOOLS_FOR_SDK:remove = "nativesdk-gcc-arm-none-eabi"

# XBee
XBEE_RESET_N_GPIO ?= "GPIOD@6"
XBEE_SLEEP_RQ_GPIO ?= ""
XBEE_TTY ?= "ttySTM2"

# Disable use of vendorfs and userfs partitions
ST_VENDORFS = "0"
ST_USERFS   = "0"

# Boot artifacts to be copied from the deploy dir to the installer ZIP
BOOTABLE_ARTIFACTS = " \
    ${@oe.utils.ifelse(d.getVar('TRUSTFENCE_SIGN') == '1', 'tf-a-ccmp25-dvk-optee-emmc${SIGN_SUFFIX}.stm32', \
                                                           'tf-a-ccmp25-dvk-optee-emmc.stm32')} \
    metadata-ccmp25-dvk.bin \
    ${@oe.utils.ifelse(d.getVar('TRUSTFENCE_SIGN') == '1', 'fip-ccmp25-dvk-optee-emmc${SIGN_SUFFIX}.bin', \
                                                           'fip-ccmp25-dvk-optee-emmc.bin')} \
"

# Per-machine DISTRO_FEATURES customization
MACHINE_DISTRO_FEATURES_ADD = "efi optee"
MACHINE_DISTRO_FEATURES_REMOVE = "x11"

# Default ROOTFS max size for image being built to this value
IMAGE_ROOTFS_MAXSIZE = "2621440"

# Default overlayfs_etc mount point and type
OVERLAYFS_ETC_MOUNT_POINT ?= "/mnt/data"
OVERLAYFS_ETC_DEVICE ?= "/dev/mmcblk0p11"
OVERLAYFS_ETC_FSTYPE ?= "ext4"
