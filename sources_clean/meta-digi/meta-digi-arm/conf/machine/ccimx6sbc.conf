#@TYPE: Machine
#@NAME: ConnectCore 6 Single Board Computer.
#@DESCRIPTION: Machine configuration for Digi's ConnectCore 6 SBC.

DIGI_SOM = "ccimx6"

# Contains the ConnectCore 6 module.
include conf/machine/include/ccimx6.inc

# Wireless external module
WIRELESS_MODULE:append = " ${@oe.utils.conditional('HAVE_WIFI', '1', 'kernel-module-qualcomm', '', d)}"
HAS_WIFI_VIRTWLANS = "true"

# Firmware
MACHINE_FIRMWARE:append = " ${@oe.utils.conditional('HAVE_BT', '1' , 'firmware-atheros-ar3k firmware-qualcomm-qca6564-bt', '', d)}"
MACHINE_FIRMWARE:append = " ${@oe.utils.conditional('HAVE_WIFI', '1' , 'firmware-atheros-ath6kl firmware-qualcomm-qca6564-wifi', '', d)}"

# U-Boot configurations
# Last one is the default (the one the symlinks point at)
UBOOT_CONFIG ??= "ccimx6dlsbc512MB ccimx6dlsbc ccimx6qsbc2GB ccimx6qsbc512MB ccimx6qsbc"
UBOOT_CONFIG[ccimx6dlsbc512MB] = "ccimx6dlsbc512MB_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
UBOOT_CONFIG[ccimx6dlsbc] = "ccimx6dlsbc_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
UBOOT_CONFIG[ccimx6qsbc2GB] = "ccimx6qsbc2GB_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
UBOOT_CONFIG[ccimx6qsbc512MB] = "ccimx6qsbc512MB_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
UBOOT_CONFIG[ccimx6qsbc] = "ccimx6qsbc_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
# Index of USB device for installer
UBOOT_INSTALL_USB_INDEX = "0"
# Index of microSD device for installer
UBOOT_INSTALL_SD_INDEX = "1"

KERNEL_DEVICETREE ?= " \
    imx6dl-ccimx6sbc.dtb \
    imx6dl-ccimx6sbc-w.dtb \
    imx6dl-ccimx6sbc-wb.dtb \
    imx6q-ccimx6sbc.dtb \
    imx6q-ccimx6sbc-w.dtb \
    imx6q-ccimx6sbc-wb.dtb \
    imx6q-ccimx6sbc-wb-ldo-bypass.dtb \
    imx6q-ccimx6sbc-id129.dtb \
    imx6q-ccimx6sbc-id130.dtb \
    imx6dl-ccimx6sbc-id131.dtb \
"

SERIAL_CONSOLES ?= "115200;ttymxc3"

# Bluetooth tty
BT_TTY ?= "ttymxc1"

# XBee
XBEE_TTY ?= "ttymxc4"

# Boot artifacts to be copied from the deploy dir to the installer ZIP
BOOTABLE_ARTIFACTS = " \
    u-boot##SIGNED##-ccimx6dlsbc512MB.imx \
    u-boot##SIGNED##-ccimx6dlsbc.imx \
    u-boot##SIGNED##-ccimx6qsbc2GB.imx \
    u-boot##SIGNED##-ccimx6qsbc512MB.imx \
    u-boot##SIGNED##-ccimx6qsbc.imx \
"

# Default overlayfs_etc mount point and type
OVERLAYFS_ETC_MOUNT_POINT ?= "/mnt/data"
OVERLAYFS_ETC_DEVICE ?= "/dev/mmcblk0p7"
OVERLAYFS_ETC_FSTYPE ?= "ext4"
